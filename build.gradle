buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

version = "2.0.0"
group = "team.covertdragon"
archivesBaseName = "MooncakeCraft"

if (System.getenv().BUILD_NUMBER != null) {
    version += "-build${System.getenv().BUILD_NUMBER}"
}

repositories {
    maven {
        name "jei"
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "jared"
        url = "http://maven.blamejared.com"
    }
}

minecraft {
    version = "1.12.2-14.23.5.2836"
    runDir = "run"

    mappings = "stable_39"
    
    replace "@VERSION@", project.version
}

dependencies {
    compile "mezz.jei:jei_1.12.2:4.13.1.220"

    deobfCompile("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.9.6") {
        exclude group: 'org.ow2.asm'
        exclude group: 'com.google.code.gson'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
